plugins {
    id 'com.gradle.plugin-publish' version '0.14.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.quality' version '4.6.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.8"
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.9'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation 'ru.vyarus:gradle-pom-plugin:2.1.0'

    testImplementation('org.spockframework:spock-core:2.0-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'com.gradle.publish:plugin-publish-plugin:0.14.0'
}

group = 'ru.vyarus'
description = 'Gradle Java-lib plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

signing {
    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
    sign publishing.publications.maven
}

tasks.withType(Sign).configureEach {
    onlyIf { !version.endsWith("SNAPSHOT") }
}

gradlePlugin {
    plugins {
        javaLibPlugin {
            id = 'ru.vyarus.java-lib'
            displayName = project.description
            implementationClass = 'ru.vyarus.gradle.plugin.lib.JavaLibPlugin'
        }
    }
}
pluginBundle {
    description = 'Common gradle configuration for Java or Groovy library'
    tags = ['java', 'groovy', 'lib']

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

// disable gradle metadata publishing (because it confuse a lot)
tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true
